### Run the following queries in Blazegraph:

PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/> 
PREFIX dcterms:  <http://purl.org/dc/terms/>   
PREFIX dctypes: <http://purl.org/dc/dcmitype/>
#prefix with new ontology
PREFIX : <http://www.semanticwebcourse.org/seminar8#>

# Retrieve the IRI and the price of all pizzas made by Pizzeria Vesuvio, ordered by price

SELECT ?iri ?price
WHERE {
  ?iri a :Pizza	;
         :wasMadeBy :pizzeriaVesuvio	;
         :hasPrice ?price	.
      } 
ORDER BY ?price

# Retrieve the IRI and the price of all pizzas that have been bought by AliceBrown or JohnSmith, filtered by price > 5€
SELECT ?iri ?price
WHERE {
  ?iri a :Pizza	;
         :hasPrice	?price	.
  {?iri	:wasBoughtBy	:aliceBrown} UNION {?iri :wasBoughtBy	:johnSmith}
  FILTER (?price > 5)
}

# Compute the total price of pizzas bought by AliceBrown

SELECT (SUM(?price) AS ?sum)
WHERE	{
  ?iri a :Pizza	;
         :wasBoughtBy :aliceBrown	;
         :hasPrice	?price	.
  }

# Compute the average price of pizzas with red base made by Pizzeria Vesuvio

SELECT (AVG(?price) AS ?avg)
WHERE {
  ?iri a :Pizza	;
         :wasMadeBy	:pizzeriaVesuvio	;
         :hasBase	?base	;
         :hasPrice	?price	.
  ?base a :RedBase	.
  }

# Retrieve the labels of people who have bought a total of more than 10€ of pizzas (use GROUP  BY and HAVING) 

SELECT ?label	
WHERE {
  ?iri a foaf:Person	;
         rdfs:label ?label	;
         :hasBought	?pizza	.
  ?pizza a :Pizza	;
           :hasPrice ?price	.
       
}
GROUP BY (?label) HAVING (SUM(?price) > 10)

# Compute the average price of each subclass of named pizza and filter the result by average < 6€

SELECT ?pizzaclass (AVG(?price) AS ?avg) 
WHERE {
   ?pizzaclass rdfs:subClassOf	:NamedPizza	.
  ?pizza a ?pizzaclass	;
           :hasPrice	?price	.
	
} GROUP BY ?pizzaclass HAVING (?avg < 6)

# Compute the average price of pizzas made by Pizzeria La Torre if the price of each pizza is reduced by 1€ (use BIND without GROUP  BY)

SELECT (AVG(?newprice) AS ?avg)
WHERE {
  ?pizza a :Pizza;
           :wasMadeBy :pizzeriaLaTorre;
           :hasPrice ?price	.
  BIND ((?price - 1) AS ?newprice)
}

# Compute the total price of pizzas bought by Alice Brown if a discount of 10% is applied on the price of each pizza

SELECT (SUM(?newprice) AS ?sum)
WHERE {
  ?pizza a :Pizza	;
           :wasBoughtBy :aliceBrown;
           :hasPrice	?price	.
  BIND ((?price - ((10*?price)/100)) AS ?newprice)
}